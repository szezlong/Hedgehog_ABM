__includes ["go_procedures//fence_behavior.nls" "go_procedures//food_behavior.nls" "go_procedures//nest_behavior.nls"]

extensions [table]

to go
  if not isEndState [
    ask hedgehogs [
      if not stay-in-nest [
        let old-position patch-here
        ifelse nest != 0 [ set distance-to-nest distance nest ] [ set distance-to-nest -1 ]
        
        update-state-variables
        
        if abs (heading - last-heading) = 180 [
          set flags lput "rotated-180" flags
        ]
        set last-heading heading
        
        if patch-ahead 1 != nobody and [pcolor] of patch-ahead 1 = gray [
          set flags lput "road-ahead" flags
        ]

        (qlearningextension:learning)

        
        ask patch-here [
          set visit-count visit-count + 1
        ]
        set distance-traveled distance-traveled + distance old-position
      ]
      
      if current-time > (night-duration * 0.8) [
        if any? patches in-radius 4 with [ self = [nest] of myself ] [
          move-to nest ;;zakładam że zdążyłby wrócić
        ]
        set stay-in-nest true
      ]
      if current-time + 1 >= night-duration and patch-here != nest[
        build-new-nest
      ]
          show flags
      
    ]
    update-visited-patches
    set current-time current-time + 1

    tick
    ]
end

to cross-street
  ask hedgehogs [
    ifelse member? "road-ahead" flags [
      let direction decide-direction
      move-along-fencing direction
      fd 0.1
      set flags lput "cross-street-success" flags
    ] [
      set flags lput "cross-street-fail" flags
    ]
  ]
end

to update-visited-patches
  ask hedgehogs [
    let current-patch patch-here
    if not member? current-patch visited-patches [
      set visited-patches lput current-patch visited-patches
      if length visited-patches > hedgehog-memory [
        set visited-patches but-first visited-patches
      ]
    ]
  ]
end

to next-night ;;czy nie jest zbędne mając już "reset-episode"?
  while [not isEndState] [
    go
  ]
  reset-episode 
end

to face-patch [patch-to-face]
  face patch-to-face
  let angle heading
  let closest-angle first possible-angles
  let min-difference abs (closest-angle - angle)
  foreach possible-angles [ x ->
    let difference abs (x - angle)
    if difference < min-difference [
      set min-difference difference
      set closest-angle x
    ]
  ]
  set heading closest-angle
end